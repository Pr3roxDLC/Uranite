buildscript {
    repositories {
        maven { url = "https://plugins.gradle.org/m2" }
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.32'
        classpath 'org.owasp:dependency-check-gradle:7.1.1'
    }
}

plugins {
    id 'java'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.owasp.dependencycheck'

group = 'me.pr3'
version = '1.0-SNAPSHOT'
archivesBaseName = 'uranite'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'stable', version: '39-1.12'
}

configurations {
    jarLibs
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spongepowered.org/repository/maven-public' }
    maven { url "https://maven-eclipse.github.io/maven" }
    maven { url "https://jitpack.io" }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'
    jarLibs("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
    // lol why does this work and is there a better way?
    // https://github.com/kami-blue/client/blob/5a9e51a124cf18ec2e73b63a8dc2e2fbb0d3a43a/build.gradle#L112
    annotationProcessor('org.spongepowered:mixin:0.8.2:processor') {
        exclude module: 'gson'
    }

    compileOnly 'com.google.code.gson:gson:2.9.0'
    compileOnly 'org.jetbrains:annotations:23.0.0'

    jarLibs 'cc.neckbeard:java-utils:1.4.1'
    jarLibs 'club.minnced:discord-webhooks:0.8.2'
    implementation configurations.jarLibs

    implementation 'org.reflections:reflections:0.10.2'
    implementation 'com.github.Pr3roxDLC:Hipster-Catching:v1.0.0'
    implementation group: 'com.google.inject', name: 'guice', version: '4.0'

    // For some reason the api at v1.2.15 exposes remapped values and breaks the build, so we use v1.2.14
    implementation 'com.github.cabaletta:baritone:v1.2.14'
}

processResources {
    inputs.property "version", project.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    defaultObfuscationEnv 'searge'
    add sourceSets.main, 'mixins.uranite.refmap.json'
}

sourceSets {
    main {
        ext.refMap = "mixins.uranite.refmap.json"
    }
}

dependencyCheck {
    failBuildOnCVSS=4.0
    cveValidForHours=1
}
check.dependsOn dependencyCheckAnalyze

jar {
    manifest {
        attributes(
                'MixinConfigs': 'mixins.uranite.json',
                'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': 0,
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLCorePlugin': 'me.pr3.uranite.impl.base.mixin.BaseMixinLoader',
                'ForceLoadAsMod': 'true'
        )
    }
    from {
        configurations.jarLibs.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
jar.finalizedBy('reobfJar')